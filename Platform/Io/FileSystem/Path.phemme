module Io.FileSystem.Path {
  import Phemme.Core
  import Data.Boolean

  ffi """
  var $path      = require('path')
  var $normalise = $path.normalize
  var $join      = $path.join
  """

  export data Path = Relative
                   | Root
                   | _ ^Path? \ _ ^Segment?

  export $a Segment? => ffi "/[^\\/]/.test($a)"

  implement Equality for Path {
    method $this === $that ^Path? => case $this, $that of
      | Relative, Relative => true
      | Root,     Root     => true
      | $as \ $a, $bs \ $b => ($a === $b) && ($as === $bs)
      | _,        _        => false
  }

  implement Representable for Path {
    method $this describe => case $this of
      | Relative => "."
      | Root     => $normalise("/")
      | $as \ $a => $join($as describe, $a)
  }

  implement Parseable for Path {
    method _ parse: $text => ffi """$p.split('/').reduce(function(xs, x) {
                                    return $self['\\'](xs, x)
                                  }, $top)"""
                                  where $p   = $normalise($text)
                                      | $top = if ffi "/^\\//.test($p)" then Root
                                                                        else Relative
  }

  implement Semigroup for Path {
    method $this + $that ^Path? => Path parse: $join($this describe, $that describe)
  }

  implement Monoid for Path {
    method empty => Relative
  }

  export $this ^Path? parent => case $this of
    | Root     => Root
    | Relative => Relative \ ".."
    | $as \ $a => $as

  export $this ^Path? filename => case $this of
    | $as \ $a => $a Just
    | _        => Nothing

  export $this ^Path? extension => case $this of
    | $as \ $a => let $xs = ffi "$a.split('.')"
                  in
                    if ffi "$xs.length > 0" then ffi "$xs[$xs.length - 1]" Just
                                           else Nothing
    | _        => Nothing

  export $this ^Path? relative? => case $this of
    | Root     => false
    | Relative => true
    | $as \ $a => $as relative?

  export $this ^Path? absolute? => not($this relative?)
}
