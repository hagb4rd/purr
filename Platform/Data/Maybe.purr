module Data.Maybe is

  import Purr.Core
  
  # -- Implicitly: data Maybe = Nothing | Just(_)
  export ( Nothing, Just, Maybe, Maybe? )
  
  implement Equality for Maybe with
    method $this === $that ^Maybe? => match $this, $that with
                                      | Nothing, Nothing => true
                                      | $a Just, $b Just => $a == $b
                                      | _, _             => false
                                      end
  end
  
  implement To-String for Maybe with
    method $this to-string => match $this with
                              | Nothing => "Nothing"
                              | $a Just => "Just(" + $a to-string + ")"
                              end
  end
  
  implement Semigroup for Maybe with
    method $this + $that ^Maybe? => match $this, $that with
                                    | $a Just, $b Just => ($a + $b) Just
                                    | _, _             => Nothing
                                    end
  end
  
  implement Monoid for Maybe with
    method $this empty => Nothing
  end
  
  implement Functor for Maybe with
    method $this map: $f => match $this with
                            | Nothing => Nothing
                            | $a Just => $f($a) Just
                            end
  end
  
  implement Applicative for Maybe with
    method $protocol of: $value => value Just
    method $this apply-to: $that => match $this with
                                    | Nothing => $that
                                    | $f Just => $that map: $f
                                    end
  end
  
  implement Monad for Maybe with
    method $this chain: $f => match $this with
                              | Nothing => Nothing
                              | $a Just => $f($a)
                              end
  end
  
  implement Foldable for Maybe with
    method $this fold-right: $f from: $b => match $this with
                                            | Nothing => $b
                                            | $a Just => $f($a)
                                            end
  end
  
  implement Alternative for Maybe with
    method none => Nothing
    method $this <|> $that => match $this with
                              | Nothing => $that
                              | _       => $this
                              end
  end

end